plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.jetbrains.kotlin.jvm' version '1.4.20'
    id 'edu.sc.seis.launch4j' version '2.4.6'
}

sourceCompatibility = 11

group rootProject.group
version rootProject.version

mainClassName = 'com.antyzero.catchme.gui.CatchMeGui'

javafx {
    version = "15.0.1"
    modules = [ 'javafx.controls' ]
}

launch4j {
    headerType='console'
    outfile = "catchme.exe"
    icon = "${projectDir}/src/main/resources/images/fish.ico"
    jar = '../libs/catchme-1.0-SNAPSHOT.jar'
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation project(':core')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': "com.antyzero.catchme.gui.CatchMeGui"
    }
    baseName = 'catchme'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

tasks.createExe.configure {
    dependsOn fatJar
}